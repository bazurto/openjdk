
// run: gradle -Pver=17.0.4.1 release

ext {
    ver = project.getProperty("ver")
    // https://adoptium.net/temurin/releases
    versionMap = [
        "17.0.4.1" : [
            "linux-amd64" : [
                url : "https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.4.1%2B1/OpenJDK17U-jdk_x64_linux_hotspot_17.0.4.1_1.tar.gz",
                downloadFileName : "OpenJDK17U-jdk_x64_linux_hotspot_17.0.4.1_1.tar.gz",
            ],
            "mac-amd64" : [
                url : "https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.4.1%2B1/OpenJDK17U-jdk_x64_mac_hotspot_17.0.4.1_1.tar.gz",
                downloadFileName : "OpenJDK17U-jdk_x64_mac_hotspot_17.0.4.1_1.tar.gz",
            ],
            "windows-amd64" : [
                url: "https://github.com/adoptium/temurin17-binaries/releases/download/jdk-17.0.4.1%2B1/OpenJDK17U-jdk_x64_windows_hotspot_17.0.4.1_1.zip",
                downloadFileName: "OpenJDK17U-jdk_x64_windows_hotspot_17.0.4.1_1.zip",
            ],
        ]
    ]
}

task release(dependsOn: ['asset']) {
    doLast {
        def assetList = []
        versionMap[ver].each { platform, vals ->
            def assetName = "openjdk-${platform}-v$ver"
            def assetFileName = "${buildDir}/downloaded/${assetName}.tgz"
            assetList += assetFileName
        }
        shell(cmd: "gh release delete -y v$ver", throw: false)
        shell(cmd: "git push origin :refs/tags/v$ver", throw: false)
        shell(cmd: "git tag -d v$ver", throw: false)
        shell(cmd: "gh release create --generate-notes -t v$ver v$ver " + assetList.join(" "))
    }
}

task asset(dependsOn: ['downloadFile']) {
    doLast {
        versionMap[ver].each { platform, vals ->
            def downloadFileName = vals['downloadFileName']
            def assetName = "openjdk-${platform}-v$ver"
            def assetDir = "${buildDir}/downloaded/$assetName"
            def assetFileName = "${buildDir}/downloaded/${assetName}.tgz"
            def fileName = "${buildDir}/downloaded/${downloadFileName}"
            def isMac = false
            if (platform.startsWith("mac")) {
                isMac = true
            }

            // uncompress to asset dir
            if (!file(assetDir).exists()) {
                if (fileName.contains('.zip')) { // unzip windows
                    shell(cmd:"unzip $fileName", dir:"${buildDir}/downloaded" )
                } else {
                    shell(cmd:"tar xvfz $fileName", dir:"${buildDir}/downloaded" )
                }
                shell(cmd:"mv jdk-17.0.4.1+1 $assetDir", dir:"${buildDir}/downloaded")
            }

            //
            writeBzFile("$assetDir/.bz", isMac)
            writeBzFile("$assetDir/.bz.lock", isMac)

            // create asset tgz
            if (!file(assetFileName).exists()) {
                shell(cmd:"tar cvfz ${assetFileName}.tmp .", dir: assetDir)
                shell(cmd:"mv ${assetFileName}.tmp ${assetFileName}", dir: "${buildDir}/downloaded")
            }
        }
    }
}



task downloadFile() {
    doLast {
        versionMap[ver].each { platform, vals ->
            def downloadUrl = vals['url']
            def downloadFileName = vals['downloadFileName']
            if (!file("${buildDir}/downloaded/${downloadFileName}").exists()) {
                shell(cmd:"mkdir -p ${buildDir}/downloaded")
                shell(cmd:"wget -O ${buildDir}/downloaded/${downloadFileName}.tmp ${downloadUrl}")
                shell(cmd:"mv ${buildDir}/downloaded/${downloadFileName}.tmp ${buildDir}/downloaded/${downloadFileName}")
            }
        }
    }
}

def shell(opts = [:]) {
    if (!opts.dir) { opts.dir = project.projectDir }
    if (!opts.env) { opts.env = System.env.entrySet().stream().map { "$it.key=$it.value"  }.collect().asList() }
    if (!opts.containsKey("throw")) { opts.throw = true }
    opts.dir = file(opts.dir)

    println opts.cmd
    def proc = opts.cmd.execute(opts.env, opts.dir)
    proc.consumeProcessOutput(System.out, System.err)
    if (proc.waitFor() != 0) {
        if (opts.throw) {
            throw new RuntimeException("command `$opts.cmd` returned error")
        }
    }
}

def writeBzFile(f, isMac) {
    def macDir = ""
    if (isMac) {
        macDir = "/Contents/Home"
    }

    def m = [
        "env": [
            "JAVA_HOME": '${DIR}' + macDir,
        ],
        "binDir": '${JAVA_HOME}/bin',
    ]


    def builder = new groovy.json.JsonBuilder(m)
    file(f).text = builder.toPrettyString()
}

